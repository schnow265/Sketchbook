plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

release_base_name = project.release_base_name
version = project.mod_version
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	//maven { url 'https://maven.blamejared.com' }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Uncomment the following line to enable the deprecated Fabric API modules.
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		// This will only be enabled on Dev
		Snapshots(MavenPublication) {
			artifactId snapshot_base_name
			version = mod_version
			groupId = maven_group
			from components.java
			ext.repo = 'snapshots'
		}

		// This will only be enabled on release
		Release(MavenPublication) {
			artifactId = release_base_name
			version = mod_version
			groupId = maven_group
			from components.java
			ext.repo = 'release'
		}

		pkshop(MavenPublication) {
			artifactId = release_base_name
			version = mod_version
			groupId = maven_group
			from components.java
			ext.repo = 'packageshop'
		}
	}

	repositories {
		maven {
			name "release"
			url "https://maven.pkg.github.com/schnow265/sketchbook"
			credentials {
				username = githubUsername
				password = ghtoken
			}
		}
		maven {
			name "snapshots"
			url "https://maven.pkg.github.com/schnow265/sketchbook"
			credentials {
				username = githubUsername
				password = ghtoken
			}
		}
		maven {
			name "packageshop"
			url "https://maven.pkg.github.com/schnow265/schnow265"
			credentials {
				username = githubUsername
				password = ghtoken
			}
		}
	}

	afterEvaluate {
		tasks.withType(PublishToMavenRepository) { task ->
			if (task.publication.hasProperty('repo') && task.publication.repo != task.repository.name) {
				task.enabled = false
				task.group = null
			}
		}
	}
}

// Modrinth Publishing
modrinth {
	token = modrinthtoken
	projectId = release_base_name
	versionNumber = mod_version
	versionType = "release" // This is the default -- can also be `beta` or `alpha`
	uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
	gameVersions = ["1.20"]
	loaders = ["fabric"]
	dependencies {
		required.project "fabric-api"
		optional.project "rei"
	}
}

task ReleasePublication {
	group 'Mod Publication'
	description "Publishes Mod to GitHub"

	ReleasePublication.dependsOn publishAllPublicationsToReleaseRepository
	ReleasePublication.dependsOn modrinth

	doLast {
		println 'Mods Published! Get ready to Download!'
	}
}

task SnapshotPublication {
	group 'Mod Publication'
	description 'Publish to Snapshot Repo'

	SnapshotPublication.dependsOn publishAllPublicationsToSnapshotsRepository

	doLast {
		println('Published to snapshot maven')
	}
}